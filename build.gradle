buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath "com.palantir:jacoco-coverage:${coveragePluginVersion}"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:${coverallsPluginVersion}"
    }
}

plugins {
    id 'org.ajoberstar.git-publish' version '0.3.1'
}

apply plugin: 'org.asciidoctor.convert'
apply plugin: 'com.palantir.jacoco-full-report'
apply plugin: 'com.github.kt3k.coveralls'

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://repo.grails.org/grails/core"
        }
        maven {
            url 'https://repo.spring.io/release/'
        }
    }
}


dependencies {
    // https://mvnrepository.com/artifact/io.spring.asciidoctor/spring-asciidoctor-extensions
    asciidoctor group: 'io.spring.asciidoctor', name: 'spring-asciidoctor-extensions', version: '0.1.1.RELEASE'

}

String currentVersion = '0.6.0'

version = currentVersion

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'jacoco'
    apply plugin: 'idea'

    group = 'com.agorapulse'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
       jcenter()
    }

    // don't forget to update version in CaseReportFormSerializer
    version = currentVersion

    processResources {
        filesMatching('**/org.codehaus.groovy.runtime.ExtensionModule') {
            filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [VERSION: version])
        }
    }

    jar {
        manifest.attributes provider: 'gradle'
    }

    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }

    jacocoTestReport {
        reports {
            xml{
                enabled true
            }
            html {
                enabled true
            }
        }
    }


    if (it.name.startsWith('examples/')) {
        // no style checks and publishing for examples
        return
    }

    apply plugin: 'checkstyle'
    apply plugin: 'codenarc'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
    }

    codenarc {
        configFile = rootProject.file('config/codenarc.xml')
        toolVersion = codenarcVersion
    }

    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    // publishing
    publishing {
        publications {
            groovyMaven(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }

                artifact javadocJar {
                    classifier "javadoc"
                }
            }
        }
    }

// set bintrayUser & bintrayKey in gradle.properties
    bintray {
        user = getPropertyOrUseDefault('bintrayUser', 'fake_user')
        key = getPropertyOrUseDefault('bintrayKey', 'fake_key')
        publications = ['groovyMaven']

        def projectName = project.name
        def projectDescription = project.description

        pkg {
            websiteUrl = 'http://agorapulse.github.io/dru/'
            issueTrackerUrl = 'https://github.com/agorapulse/dru/issues'
            vcsUrl = 'https://github.com/agorapulse/agorapulse/dru.git'

            repo = 'libs' // or your repo name
            userOrg = 'agorapulse'
            name = projectName       // somehow project.* doesn't work in this closure
            desc = projectDescription
            licenses = ['Apache-2.0']
        }
        // dryRun = true // whether to run this as dry-run, without deploying
    }

}

// asciidoctor publishing
asciidoctor {

    sourceDir = file('docs')

    resources {
        from(sourceDir) {
            include 'css/**', 'images/**'
        }
    }

    attributes 'docinfo1': ['version': currentVersion],
            'imagesdir': 'images',
            'source-highlighter': 'highlight.js',
            'stylesdir': 'css',
            icons: 'font',
            'toc': 'left',
            version: project.version,
            'projectUrl': 'https://github.com/agorapulse/dru'
}

gitPublish {
    repoUri = 'git@github.com:agorapulse/dru.git'
    branch = 'gh-pages'

    contents {
        from file(asciidoctor.outputDir.path + '/html5')
    }
}

gitPublishCopy.dependsOn asciidoctor

tasks.jacocoFullReport.dependsOn subprojects.collect { it.tasks.check }

gradle.taskGraph.whenReady {
    coveralls {
        sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
        jacocoReportPath = "${buildDir}/reports/jacoco/jacocoFullReport/jacocoFullReport.xml"
    }
}



tasks.coveralls {
    group = 'Coverage reports'
    description = 'Uploads the aggregated coverage report to Coveralls'

    dependsOn jacocoFullReport
    onlyIf { System.env.'CI' }
}

String getPropertyOrUseDefault(String propertyName, String defaultValue) {
    hasProperty(propertyName) ? getProperty(propertyName) : defaultValue
}
